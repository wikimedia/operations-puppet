# == Define: ca::certs
#
# Installs keystores and keyfiles generated by ca-manager using
# puppet secret().
#
# === Usage
#   ca::certs { 'cassandra/services/restbase1007':
#       destination_directory => '/etc/cassandra/tls',
#       local_key_name        => 'server',
#       owner                 => 'cassandra',
#       group                 => 'cassandra',
#   }
#
# === Parameters
#
# [*title*]
#
# [*destination_directory*]
#
# ...
define ca::certs(
    $destination_directory,
    $local_key_name = undef,
    $owner          = 'root',
    $group          = 'root',
    $keystores      = true,
    $keyfiles       = true,
) {


    # cassandra/services/restbase1007: $key_name = restbase1007
    $key_name = basename($title)
    # If $local_key_name not provided, default to $key_name.
    $_local_key_name = $local_key_name ? {
        undef => $key_name,
        default => $local_key_name,
    }

    # Base secret path is the directory above where this
    # particular key was generated.
    # cassandra/services/restbase1007: $base_secret_path = cassandra/services.
    # This is to build the secrets path, and should be equivalent
    # to the base_directory (in the private secrets module) as provided
    # to ca-manager when the key files were generated.
    $base_secret_path = dirname($title)

    file { $destination_directory:
        ensure  => 'directory',
        owner   => $owner,
        group   => $group,
        mode    => '0400',
    }

    # Java key Keystore
    # TODO: standardize on file extensions!  Why .kst -> .key???
    file { "${destination_directory}/${_local_key_name}.key":
        content => secret("${base_secret_path}/${key_name}/${key_name}.kst"),
        owner   => $owner,
        group   => $group,
        mode    => '0400',
        require => File[$destination_directory],
    }

    # Java CA trust keystore
    file { "${destination_directory}/${_local_key_name}.trust":
        content => secret("${base_secret_path}/truststore"),
        owner   => $owner,
        group   => $group,
        mode    => '0400',
        require => File[$destination_directory],
    }

    # CA Cert
    file { "${destination_directory}/rootCa.crt":
        content => secret("${base_secret_path}/rootCa.crt"),
        owner   => $owner,
        group   => $group,
        mode    => '0400',
        require => File[$destination_directory],
    }
}