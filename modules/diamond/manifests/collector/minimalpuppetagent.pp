# == Define: diamond::collector::minimalpuppetagent
#
# Configures a minimal puppet agent collector
# that collects just time since last puppet run
# and total time it took for puppet to run
#
define diamond::collector::minimalpuppetagent(
    String $sudo_flavor = 'sudo',
) {
    require_package('python-yaml')

    # Diamond user needs sudo to access last_run_summary.yaml file generated by
    # puppet, since /var/lib/puppet doesn't have +x set
    sudo::user { 'diamond_sudo_for_puppet':
        user        => 'diamond',
        privileges  => ['ALL=(puppet) NOPASSWD: /bin/cat /var/lib/puppet/state/last_run_summary.yaml'],
        sudo_flavor => $sudo_flavor,
    }

    diamond::collector { 'MinimalPuppetAgent':
        source  => 'puppet:///modules/diamond/collector/minimalpuppetagent.py',
        require => Package['python-yaml', 'diamond'],
    }
}
