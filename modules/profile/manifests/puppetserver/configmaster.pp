# SPDX-License-Identifier: Apache-2.0
# Create a simple TLS proxy and webserver to expose the canonical SHA1 values
# for puppet and labs-puppet that config-master will use.
class profile::puppetserver::configmaster (
    Stdlib::Unixpath $document_root = lookup('profile::puppetserver::configmaster::document_root', { 'default_value' => '/srv/config-master'}),
    String $server_name = lookup('profile::puppetserver::configmaster::server_name'),
) {
    include profile::tlsproxy::envoy
    include profile::httpd

    # Note: the mode is 755 since the SHA1 files generated by
    # puppet-merge.py will need to be written when running the script.
    file { $document_root:
        ensure => directory,
        mode   => '0755',
    }

    httpd::site { 'config-master':
        ensure   => present,
        priority => 50,
        content  => template('profile/puppetserver/configmaster/config-master.conf.erb'),
    }
}
