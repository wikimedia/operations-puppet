# Exim filter

if first_delivery then
	# Remove headers that control OTRS - we don't want these
	headers remove X-OTRS-Priority:X-OTRS-Queue:X-OTRS-Lock:X-OTRS-Ignore:X-OTRS-State
	if $acl_m0 is not "trusted relay" then
		# Remove any SpamAssassin headers and add local ones
		headers remove X-Spam-Score:X-Spam-Report:X-Spam-Checker-Version:X-Spam-Status:X-Spam-Level:X-Spam-Flag
	endif
	if $acl_m0 is not "" and $acl_m0 is not "trusted relay" then
		headers add "X-Spam-Score: $acl_m0"
		headers add "X-Spam-Report: $acl_m1"
		# Add header for OTRS filters
		if $acl_m1 is not "" and $acl_m1 begins "yes" then
			headers add "X-Spam-Flag: YES"
		# overload X-Spam-Flag since OTRS doesn't do numeric comparison
		elif $acl_m3 is not "" and $acl_m3 is above 20 then
			headers add "X-Spam-Flag: MAYBE"
		else
			headers add "X-Spam-Flag: NO"
		endif
		# add a hook for OTRS to filter list mail
		# stewards is exempt. T224404
		if
			($message_headers contains "\nList-Id:" or
			$message_headers contains "\nList-Help:" or
			$message_headers contains "\nList-Subscribe:" or
			$message_headers contains "\nList-Unsubscribe:" or
			$message_headers contains "\nList-Post:" or
			$message_headers contains "\nList-Owner:" or
			$message_headers contains "\nList-Archive:") and
			$header_precedence: does not match "^(bulk|junk|list)" and
			$header_to: does not match "stewards@wikimedia.org"
		then
			headers remove Precedence
			headers add "Precedence: bulk"
		endif
	endif
endif
