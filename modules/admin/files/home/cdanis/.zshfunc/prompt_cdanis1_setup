# cdanis1 prompt theme
# like adam1 but
#   - never abbrevs paths with ...
#   - chooses if prompt spans lines based on terminal width
#   - super-cool 😻 EMOJI PROMPT 😻 BY THE HOUR 🕐🕑🕒🕓🕔🕕🕖🕗🕘🕙🕚🕛

prompt_cdanis1_help () {
  cat <<'EOF'
This prompt is color-scheme-able.  You can invoke it thus:

  prompt cdanis1 [<color1> [<color2> [<color3>]]]

where the colors are for the user@host background, current working
directory, and current working directory if the prompt is split over
two lines, respectively.  The default colors are blue, cyan and green.
This theme works best with a dark background.
EOF
}

HOMETZ='America/New_York'

# Wrapper needed at least for zsh 5.3.1 (Debian stretch)
cd_strftime() {
  strftime "$1" $EPOCHSECONDS
}

prompt_cdanis1_setup () {
  setopt localoptions nowarncreateglobal
  prompt_cdanis1_color1=${1:-'blue'}
  prompt_cdanis1_color2=${2:-'cyan'}
  prompt_cdanis1_color3=${3:-'green'}

  base_prompt="%(0?,💙,💔)%K{%(!.red.$prompt_cdanis1_color1)}%n@$(hostname -f | sed 's/\.local\(domain\)\?//')%k "
  post_prompt="%b%f%k"

  setopt localoptions extendedglob
  base_prompt_no_color="${base_prompt//(%K{[^\\\}]#\}|%k)/}"
  post_prompt_no_color="${post_prompt//(%K{[^\\\}]#\}|%k)/}"

  zmodload zsh/datetime
  # the delta between the server's 'local' time and my usual local time
  prompt_tz_offset=$(( ($(cd_strftime %H) - $(TZ="$HOMETZ" cd_strftime %H) + 24) % 24 ))

  add-zsh-hook precmd prompt_cdanis1_precmd
}

prompt_cdanis1_precmd () {
  setopt localoptions noxtrace nowarncreateglobal
  local base_prompt_expanded_no_color base_prompt_etc_no_color
  local prompt_length
  local hour_prompt hour
  local minute clock_prompt
  local clocks_top clocks_bottom

  base_prompt_expanded_no_color=$(print -P "$base_prompt_no_color")
  base_prompt_etc_no_color=$(print -P "$base_prompt_no_color%~")
  prompt_length=${#base_prompt_etc_no_color}
  if [[ $prompt_length -lt $(( $COLUMNS * 0.4 )) ]]; then
    path_prompt="%B%F{$prompt_cdanis1_color2}%~%F{white}"
  else
    path_prompt="%B%F{$prompt_cdanis1_color3}%~$prompt_newline%F{white}"
  fi
  # what time is it? let's find out!
  hour_prompt=""
  # you should be asleep
  for hour in {1..6} ; do
    hour_prompt+="%($((($hour + $prompt_tz_offset)%24))T,💤,)"
  done
  # time for coffee
  for hour in {7..14} ; do
    hour_prompt+="%($((($hour + $prompt_tz_offset)%24))T,☕,)"
  done
  # little late for coffee, have tea instead
  for hour in 15 16; do
    hour_prompt+="%($((($hour + $prompt_tz_offset)%24))T,🍵,)"
  done
  # how about a beer?
  for hour in {17..23} 0 ; do
    hour_prompt+="%($((($hour + $prompt_tz_offset)%24))T,🍺,)"
  done

  clock_prompt=""
  clocks_top=(🕛 🕐 🕑 🕒 🕓 🕔 🕕 🕖 🕗 🕘 🕙 🕚)
  clocks_bottom=(🕧 🕜 🕝 🕞 🕟 🕠 🕡 🕢 🕣 🕤 🕥 🕦)
  hour=$(TZ="$HOMETZ" cd_strftime %I)
  minute=$(TZ="$HOMETZ" cd_strftime %M)
  case "$minute" in
    (<45-59>|<0-15>))
      clock_prompt="$clocks_top[$(( ((12 + $hour + (($minute>44)?1:0) )%12) + 1 ))]"
    ;;
    <16-44>)
      clock_prompt="$clocks_bottom[$(( ((12 + $hour)%12) + 1 ))]"
    ;;
  esac

  PS1="$base_prompt$path_prompt $clock_prompt%(!.🙃.$hour_prompt) $post_prompt"
  PS2="$base_prompt$path_prompt %_> $post_prompt"
  PS3="$base_prompt$path_prompt ?# $post_prompt"
}

prompt_cdanis1_setup "$@"
