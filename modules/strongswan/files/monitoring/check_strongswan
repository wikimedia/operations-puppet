#!/usr/bin/perl -w

# check_strongswan
# jgage@wikimedia.org 2015-03
#
# Nagios check command for Strongswan
# Parses output of 'ipsec statusall': checks that each defined connection has a
# corresponding established Security Association.

use strict;

my $OK    = "OK";
my $EOK   = 0;
my $WARN  = "WARNING";
my $EWARN = 1;
my $CRIT  = "CRITICAL";
my $ECRIT = 2;
my $UNKN  = "UNKNOWN";
my $EUNKN = 3;

# Run the command: /usr/sbin/ipsec is a wrapper; save an exec by calling the
# equivalent non-blocking internal command directly.
my @cmdoutput = `/usr/lib/ipsec/stroke statusall-nb 2>&1`;
if ($? != 0) {
    print ("Strongswan $UNKN - ipsec statusall: @cmdoutput");
    exit($EUNKN);
}

# Sample output:
#   Connections:
#   berkelium.eqiad.wmnet-curium.eqiad.wmnet_by_ipv4:  10.64.0.169...10.64.0.170  IKEv1/2
#   berkelium.eqiad.wmnet-cp3002.esams.wmnet_by_ipv4:  10.64.0.169...10.20.0.102  IKEv1/2
#   berkelium.eqiad.wmnet-cp3001.esams.wmnet_by_ipv6:  2620::861:101:10:64:0:169...2620::862:102:10:20:0:101  IKEv1/2
#   Security Associations (2 up, 0 connecting):
#   berkelium.eqiad.wmnet-curium.eqiad.wmnet_by_ipv4[1]: ESTABLISHED 42 seconds ago, 10.64.0.169[CN=berkelium.eqiad.wmnet]...10.64.0.170[CN=curium.eqiad.wmnet]
#   berkelium.eqiad.wmnet-cp3002.esams.wmnet_by_ipv4[5]: CONNECTING, 10.64.0.169[%any]...10.20.0.102[%any]

# Parse output into a hash
my %conns;
foreach my $line (@cmdoutput) {
    if ($line =~ m/\.\.\./) {
        (my $key, my $value) = split(/: /, $line, 2);
        $key =~ s/(^.*)\[\d+\]$/$1/;
        $conns{$key}=$value;
    }
}

# Check state of each connection: If connections are neither ESTABLISHED nor
# CONNNECTING, they are not listed in the Security Associations section.
my $established=0;
my $connecting=0;
my $neither=0;
foreach my $key (keys %conns) {
    if ($conns{$key} =~ m/ESTABLISHED/) {
        $established++;
    } elsif ($conns{$key} =~ m/CONNECTING/) {
        $connecting++;
    } else {
        $neither++;
    }
}

# Return results
if ($neither != 0) {
    print ("Strongswan $CRIT - Security Associations: $established established, $connecting connecting, $neither not connected\n");
    exit($ECRIT);
} elsif ($connecting != 0) {
    print ("Strongswan $WARN - Security Associations: $established established, $connecting connecting\n");
    exit($EWARN);
} elsif ($established != 0) {
    print ("Strongswan $OK - Security Associations: $established established\n");
    exit($EOK);
} else {
    print ("No connections configured\n");
    exit($EWARN);
}
