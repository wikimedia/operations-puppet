# SPDX-License-Identifier: Apache-2.0
# client<->ats-tls metrics
# histogram trafficserver_tls_client_ttfb
# histogram trafficserver_tls_client_healthcheck_ttfb
histogram trafficserver_tls_client_ttfb by cache_status, http_status_family buckets 0.045, 0.07, 0.1, 0.15, 0.25, 0.35, 0.5, 0.75, 1.2, 3.0, 10.0, 30.0, 60.0
histogram trafficserver_tls_client_healthcheck_ttfb by cache_status, http_status_family buckets 0.045, 0.07, 0.1, 0.15, 0.25, 0.35, 0.5, 0.75, 1.2, 3.0, 10.0, 30.0, 60.0

# Skip log entries with status 000 (aborted transactions)
/time_firstbyte:(?P<client_ttfb>[0-9]+)\t.*cache_status:(?P<cache_status>[a-z-]+)\t.*http_status:(?P<http_status_family>[1-9])[0-9][0-9]\t.*uri_host:(?P<host>[0-9A-Za-z\-\.:]+)\t/ {
  trafficserver_tls_client_ttfb[$cache_status][$http_status_family] = float($client_ttfb) * 0.001
  $host =~ /^healthcheck\.wikimedia\.org$/ {
    trafficserver_tls_client_healthcheck_ttfb[$cache_status][$http_status_family] = float($client_ttfb) * 0.001
  }
}

# histogram trafficserver_tls_client_total_time
histogram trafficserver_tls_client_total_time by cache_status, http_status_family buckets 0.045, 0.07, 0.1, 0.15, 0.25, 0.35, 0.5, 0.75, 1.2, 3.0, 10.0, 30.0, 60.0

/time_total:(?P<client_total_time>[0-9]+)\t.*cache_status:(?P<cache_status>[a-z-]+)\t.*http_status:(?P<http_status_family>[1-9])[0-9][0-9]/ {
  trafficserver_tls_client_total_time[$cache_status][$http_status_family] = float($client_total_time) * 0.001
}
