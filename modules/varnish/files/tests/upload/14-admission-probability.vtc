varnishtest "Admission probability with different CL values"

server s1 {
    # CL == 0
    rxreq
    expect req.url == "/1"
    txresp -nolen -hdr "Content-Length: 0"

    # CL > 0
    rxreq
    expect req.url == "/2"
    txresp -bodylen 65536

    # No CL
    rxreq
    expect req.url == "/3"
    txresp -nolen -hdr "Transfer-Encoding: chunked"
    chunkedlen 12
    chunkedlen 0

    # CL == 0
    rxreq
    expect req.url == "/4"
    txresp -nolen -hdr "Content-Length: 0"
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_upload-frontend.vcl";
} -start

client c1 {
    txreq -url "/1" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org"
    rxresp

    expect resp.status == 200
    expect resp.http.X-Admission-Probability == <undef>

    txreq -url "/2" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org"
    rxresp

    expect resp.status == 200
    expect resp.http.X-Admission-Probability > 0
    expect resp.http.X-Admission-Probability < 1

    txreq -url "/3" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org"
    rxresp

    expect resp.status == 200
    expect resp.http.X-Admission-Probability == <undef>

    txreq -url "/4" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org"
    rxresp

    expect resp.status == 200
    expect resp.http.X-Admission-Probability == <undef>
} -run
