varnishtest "upload backends should not cache small objects"

server s1 {
    rxreq
    expect req.url == "/test-pass"
    txresp -bodylen 255

    rxreq
    expect req.url == "/test-pass"
    txresp -bodylen 255

    rxreq
    expect req.url == "/test-miss-hit"
    txresp -bodylen 1025
} -start

varnish v1 -arg "-p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='exec gcc -std=gnu99 -g -O2 -fstack-protector-strong -Wformat -Werror=format-security -Wall -pthread -fpic -shared -Wl,-x -o %o %s'" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_upload-backend.vcl";
} -start

client c1 {
    txreq -url "/test-pass" -hdr "Host: upload.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache-Int ~ "pass"
} -run

varnish v1 -expect cache_hitpass == 0

client c2 {
    txreq -url "/test-pass" -hdr "Host: upload.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache-Int ~ "pass"
} -run

varnish v1 -expect cache_hitpass == 1

client c3 {
    txreq -url "/test-miss-hit" -hdr "Host: upload.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache-Int ~ "miss"
} -run

varnish v1 -expect cache_hitpass == 1

client c4 {
    txreq -url "/test-miss-hit" -hdr "Host: upload.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache-Int ~ "hit/1"
} -run

varnish v1 -expect cache_hitpass == 1
