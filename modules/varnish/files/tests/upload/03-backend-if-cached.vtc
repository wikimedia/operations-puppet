varnishtest "If-Cached backend behavior"

server s1 {
    rxreq
    txresp -hdr "ETag: 15f0fff99ed5aae4edffdd6496d7131f" -bodylen 1024
} -start

varnish v1 -arg "-p vcc_err_unref=false -p vcc_allow_inline_c=true" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_upload-backend.vcl";
} -start

client c1 {
    # If-Cached with cache miss
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "If-Cached: 15f0fff99ed5aae4edffdd6496d7131f"
    rxresp
    expect resp.status == 412
    expect resp.reason == "Entity not in cache"
} -run

client c2 {
    txreq -url "/test" -hdr "Host: upload.wikimedia.org"
    rxresp
    expect resp.status == 200

    # If-Cached with cache hit
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "If-Cached: 15f0fff99ed5aae4edffdd6496d7131f"
    rxresp
    expect resp.status == 200

    # If-Cached with cache hit and Etag mismatch
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "If-Cached: mismatched-etag"
    rxresp
    expect resp.status == 412
    expect resp.reason == "Etag mismatch"
} -run
