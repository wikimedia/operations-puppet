varnishtest "Range requests"

server s1 {
    rxreq
    txresp -bodylen 20
    rxreq
    txresp -bodylen 20
    rxreq
    txresp -bodylen 20
    rxreq
    txresp -bodylen 20
} -start

varnish v1 -arg "-p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='exec gcc -std=gnu99 -g -O2 -fstack-protector-strong -Wformat -Werror=format-security -Wall -pthread -fpic -shared -Wl,-x -o %o %s'" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_upload-backend.vcl";
} -start

# Pleonastic precondition
varnish v1 -expect cache_miss == 0
varnish v1 -expect cache_hit == 0
varnish v1 -expect n_object == 0

client c1 {
    # Range request
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "Range: bytes=0-9"
    rxresp
    # Partial content
    expect resp.status == 206
    expect resp.http.Content-Length == 10
}

# First range request 0-9, expecting a pass
client c1 -run
varnish v1 -expect cache_miss == 0
varnish v1 -expect cache_hit == 0
varnish v1 -expect n_object == 0
varnish v1 -expect s_pass == 1

# Second range request 0-9, expecting another pass
client c1 -run
varnish v1 -expect cache_miss == 0
varnish v1 -expect cache_hit == 0
varnish v1 -expect n_object == 0
varnish v1 -expect s_pass == 2

client c2 {
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "Range: bytes=1-4"
    rxresp
    expect resp.status == 206
    expect resp.http.Content-Length == 4
}

client c2 -run
varnish v1 -expect cache_miss == 0
varnish v1 -expect cache_hit == 0
varnish v1 -expect n_object == 0
varnish v1 -expect s_pass == 3

client c3 {
    # unsatisfiable Range request
    txreq -url "/test" -hdr "Host: upload.wikimedia.org" -hdr "Range: bytes=50-100"
    rxresp
    # Varnish 4 returns a 416 - Range Not Satisfiable
    # Varnish 3 responds with a 200 and the entire selected representation
    expect resp.status == 416
    expect resp.http.Content-Length == 0
}

# Range request for bytes > CL, varnish returns the whole cached object.
client c3 -run
varnish v1 -expect cache_miss == 0
varnish v1 -expect cache_hit == 0
varnish v1 -expect n_object == 0
varnish v1 -expect s_pass == 4
