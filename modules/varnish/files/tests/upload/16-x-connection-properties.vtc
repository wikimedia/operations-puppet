varnishtest "X-Connection-Properties"

server s1 {
    rxreq
    expect req.url == "/1"

    expect req.http.X-CP-HTTP2 == "0"
    expect req.http.X-CP-TLS-Version == "TLSv1.2"
    expect req.http.X-CP-TLS-Session-Reused == 0
    expect req.http.X-CP-Key-Exchange == "prime256v1"
    expect req.http.X-CP-Auth == "ECDSA"
    expect req.http.X-CP-Cipher == "AES256-GCM-SHA384"
    expect req.http.X-CP-Full-Cipher == "ECDHE-ECDSA-AES256-GCM-SHA384"

    txresp

    rxreq
    expect req.url == "/2"

    expect req.http.X-CP-HTTP2 == "0"
    expect req.http.X-CP-TLS-Version == "TLSv1"
    expect req.http.X-CP-TLS-Session-Reused == 1
    expect req.http.X-CP-Key-Exchange == "prime256v1"
    expect req.http.X-CP-Auth == "ECDSA"
    expect req.http.X-CP-Cipher == "AES128-SHA"
    expect req.http.X-CP-Full-Cipher == "ECDHE-ECDSA-AES128-SHA"

    txresp

    rxreq
    expect req.url == "/3"

    expect req.http.X-CP-HTTP2 == "1"
    expect req.http.X-CP-TLS-Version == "TLSv1.2"
    expect req.http.X-CP-TLS-Session-Reused == 1
    expect req.http.X-CP-Key-Exchange == "X25519"
    expect req.http.X-CP-Auth == "ECDSA"
    expect req.http.X-CP-Cipher == "AES256-GCM-SHA384"
    expect req.http.X-CP-Full-Cipher == "ECDHE-ECDSA-AES256-GCM-SHA384"

    txresp

    rxreq
    expect req.url == "/4"

    expect req.http.X-CP-HTTP2 == "0"
    expect req.http.X-CP-TLS-Version == "TLSv1"
    expect req.http.X-CP-TLS-Session-Reused == 1
    expect req.http.X-CP-Key-Exchange == "RSA"
    expect req.http.X-CP-Auth == "RSA"
    expect req.http.X-CP-Cipher == "AES128-SHA"
    expect req.http.X-CP-Full-Cipher == "AES128-SHA"

    txresp

    rxreq
    expect req.url == "/5"

    expect req.http.X-CP-HTTP2 == "1"
    expect req.http.X-CP-TLS-Version == "TLSv1.2"
    expect req.http.X-CP-TLS-Session-Reused == 0
    expect req.http.X-CP-Key-Exchange == "X25519"
    expect req.http.X-CP-Auth == "ECDSA"
    expect req.http.X-CP-Cipher == "CHACHA20-POLY1305-SHA256"

    txresp

    # X-Connection-Properties not set. All CP stats must be undefined.
    rxreq
    expect req.url == "/6"

    expect req.http.X-CP-HTTP2 == <undef>
    expect req.http.X-CP-TLS-Version == <undef>
    expect req.http.X-CP-TLS-Session-Reused == <undef>
    expect req.http.X-CP-Key-Exchange == <undef>
    expect req.http.X-CP-Auth == <undef>
    expect req.http.X-CP-Cipher == <undef>

    txresp
} -start

varnish v1 -arg "-p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='exec gcc -std=gnu99 -g -O2 -fstack-protector-strong -Wformat -Werror=format-security -Wall -pthread -fpic -shared -Wl,-x -o %o %s'" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_upload-frontend.vcl";
} -start

client c1 {
    txreq -url "/1" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org" -hdr "X-Connection-Properties: H2=0; SSR=0; SSL=TLSv1.2; C=ECDHE-ECDSA-AES256-GCM-SHA384; EC=prime256v1;"
    rxresp

    txreq -url "/2" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org" -hdr "X-Connection-Properties: H2=0; SSR=1; SSL=TLSv1; C=ECDHE-ECDSA-AES128-SHA; EC=prime256v1;"
    rxresp

    txreq -url "/3" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org" -hdr "X-Connection-Properties: H2=1; SSR=1; SSL=TLSv1.2; C=ECDHE-ECDSA-AES256-GCM-SHA384; EC=X25519;"
    rxresp

    txreq -url "/4" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org" -hdr "X-Connection-Properties: H2=0; SSR=1; SSL=TLSv1; C=AES128-SHA; EC=UNDEF;"
    rxresp

    txreq -url "/5" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org" -hdr "X-Connection-Properties: H2=1; SSR=0; SSL=TLSv1.2; C=ECDHE-ECDSA-CHACHA20-POLY1305; EC=X25519;"
    rxresp

    txreq -url "/6" -hdr "X-Forwarded-Proto: https" -hdr "Host: upload.wikimedia.org"
    rxresp
} -run
