varnishtest "Maps frontend TLS redirect"

server s1 {
	rxreq
	txresp
} -start

varnish v1 -arg "-p vcc_err_unref=false" -vcl+backend {
	backend vtc_backend {
		.host = "${s1_addr}"; .port = "${s1_port}";
	}

	include "/usr/share/varnish/tests/wikimedia_maps-frontend.vcl";
} -start

client c1 {
	# Using https according to X-Forwarded-Proto
	txreq -url "/?foo=bar" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
	rxresp
	expect resp.status == 200
} -run

client c2 {
	# No X-Forwarded-Proto
	txreq -url "/?foo=bar" -hdr "Host: en.wikipedia.org"
	rxresp
	expect resp.status == 301
	expect resp.http.location == "https://en.wikipedia.org/?foo=bar"
} -run

client c3 {
	# Insecure POST
	txreq -req POST -url "/?foo=bar" -hdr "Host: en.wikipedia.org"
	rxresp
	expect resp.status == 403
} -run
