varnishtest "Add Content-Length to Chunked responses from direct backend"

server s1 {
    rxreq
    # The backend is not going to set Content-Length
    txresp -nolen -hdr "Transfer-encoding: chunked"
    chunked hello
    chunkedlen 0
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-backend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-backend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-backend.vcl";
} -start

client c1 {
    txreq -hdr "Host: phab.wmfusercontent.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    # We expect Content-Length to be set to 5 (hello) by varnish
    expect resp.http.Content-Length == 5
} -run
