varnishtest "sec-warning synth response"

server s1 {
   rxreq
   txresp
   rxreq
   txresp
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

logexpect l0 -v v1 {
    expect * 1001 VCL_call SYNTH
} -start

# with seed=55, std.random(0,100) returns 0.716
varnish v1 -cliok "debug.srandom 55"

client c1 {
    txreq -url "/wiki/Main_Page" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Connection-Properties: H2=0; SSR=0; SSL=TLSv1; C=ECDHE-ECDSA-AES128-SHA; EC=prime256v1;"
    rxresp
    expect resp.status == 302
    expect resp.http.location == "https://en.wikipedia.org/sec-warning"
    txreq -url "/sec-warning" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Connection-Properties: H2=0; SSR=0; SSL=TLSv1; C=ECDHE-ECDSA-AES128-SHA; EC=prime256v1;"
    rxresp
    expect resp.status == 200
    txreq -url "/static/images/wmf-logo.png" -hdr "Host: www.wikimedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Connection-Properties: H2=0; SSR=0; SSL=TLSv1; C=ECDHE-ECDSA-AES128-SHA; EC=prime256v1;"
    rxresp
    expect resp.status == 200
} -run

# reset the seed
varnish v1 -cliok "debug.srandom 55"

client c2 {
    txreq -url "/wiki/Main_Page" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "X-Connection-Properties: H2=0; SSR=0; SSL=TLSv1.2; C=ECDHE-ECDSA-AES128-SHA; EC=prime256v1;"
    rxresp
    expect resp.status == 200
} -run

logexpect l0 -wait
