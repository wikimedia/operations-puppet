varnishtest "Content-Length must always be sent in response to HEAD requests for docker-registry manifests T270270"

server s1 {
    rxreq
    txresp -hdr "Content-Type: application/json" -bodylen 1024
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    txreq -url "/v2/dev/stretch-php74-fpm-apache2-xdebug/manifests/0.6.1-s1" -req "HEAD" -hdr "Host: docker-registry.wikimedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp -no_obj
    expect resp.status == 200
    expect resp.http.x-cache ~ "miss"
    expect resp.http.content-length == 1024

    txreq -url "/v2/dev/stretch-php74-fpm-apache2-xdebug/manifests/0.6.1-s1" -req "HEAD" -hdr "Host: docker-registry.wikimedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp -no_obj
    expect resp.status == 200
    expect resp.http.x-cache ~ "hit"
    expect resp.http.content-length == 1024
} -run
