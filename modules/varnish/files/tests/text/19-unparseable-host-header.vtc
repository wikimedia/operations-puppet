varnishtest "Bad Request if Host is unparseable/missing"

server s1 {
    loop 6 {
        rxreq
        txresp
    }
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    # OK: decent Host header
    txreq -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # OK: decent Host header
    txreq -hdr "Host: en.m.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # OK: Host header starting with a digit
    txreq -hdr "Host: 15.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    # KO: IP address
    txreq -hdr "Host: 127.0.0.1" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 400

    # OK: healthchecks
    txreq -url "/from/pybal" -hdr "Host: varnishcheck.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.body ~ "^Varnish frontend running on cp[0-9]{4} is up$"

    txreq -url "/varnish-fe" -hdr "Host: healthcheck.wikimedia.org"
    rxresp
    expect resp.status == 200
    expect resp.body ~ "^Varnish frontend running on cp[0-9]{4} is up$"

    # KO: Missing Host header
    txreq -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 400

    # KO: Empty Host header
    txreq -hdr "Host: " -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 400

    # KO: Host header with underscore
    txreq -hdr "Host: en.wiki_pedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 400
} -run
