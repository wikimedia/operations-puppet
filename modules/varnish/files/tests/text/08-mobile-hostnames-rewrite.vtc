varnishtest "cache_text mobile hostname rewrites"

server s1 {
    rxreq
    expect req.http.x-dt-host == "www.mediawiki.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "www.wikidata.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "foundation.wikimedia.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "it.wikipedia.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "wikipedia.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "en.wikipedia.org"
    txresp

    rxreq
    expect req.http.x-dt-host == "zh-classical.wikipedia.org"
    txresp

    loop 7 {
        rxreq
        expect req.http.x-dt-host == <undef>
        txresp
    }
} -start

varnish v1 -arg "-p vcl_path=${vcl_path} -p vcc_err_unref=false -p vcc_allow_inline_c=true -p cc_command='${cc_command}'" -vcl+backend {
   backend vtc_backend {
       .host = "${s1_addr}"; .port = "${s1_port}";
   }

   include "${basepath}/usr/share/varnish/tests/wikimedia_misc-frontend.vcl";
}

# Attach the "wikimedia_misc" label to wikimedia_misc-frontend.vcl
varnish v1 -cli "vcl.label wikimedia_misc vcl1"

varnish v1 -vcl {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "${basepath}/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    txreq -url "/" -hdr "Host: m.mediawiki.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: m.wikidata.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: foundation.m.wikimedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: it.m.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: m.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: en.zero.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: zh-classical.m.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp

    txreq -url "/wiki/Main_Page" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/wiki/Main_Page"
    expect resp.status == 302

    txreq -url "/wiki/Main_Page" -hdr "User-Agent: nintendo" -hdr "Host: zh-classical.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://zh-classical.m.wikipedia.org/wiki/Main_Page"
    expect resp.status == 302

    txreq -url "/?title=foo" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/?title=foo"
    expect resp.status == 302

    txreq -url "/?title=foo&bar=baz" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/?title=foo&bar=baz"
    expect resp.status == 302

    txreq -url "/?bar=baz&title=foo" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/?bar=baz&title=foo"
    expect resp.status == 302

    txreq -url "/w/index.php?title=MyPage&action=info" -hdr "User-Agent: nintendo" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/w/index.php?title=MyPage&action=info"
    expect resp.status == 302

    txreq -url "/w/index.php?title=MyPage&action=info"  -hdr "User-Agent: firefox" -hdr "Host: simple.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.http.location == "https://simple.m.wikipedia.org/w/index.php?title=MyPage&action=info"
    expect resp.status == 302

    txreq -url "/w/index.php?title=MyPage&action=info"  -hdr "User-Agent: firefox" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/w/index.php?title=MyPage&action=info"
    expect resp.status == 302

    txreq -url "/w/index.php?title=MyPage&action=info"  -hdr "User-Agent: SMART-TV SamsungBrowser" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.http.location == "https://en.m.wikipedia.org/w/index.php?title=MyPage&action=info"
    expect resp.status == 302

    txreq -url "/wiki/Sonido" -hdr "User-Agent: firefox" -hdr "Host: es.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.status == 200

    txreq -url "/wiki/Sonido" -hdr "User-Agent: SMART-TV SamsungBrowser" -hdr "Host: es.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.status == 200

    txreq -url "/wiki/Sonido" -hdr "User-Agent: SMART-TV SamsungBrowser" -hdr "Host: es.wikipedia.org" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200

    txreq -url "/wiki/Saturn"  -hdr "User-Agent: Googlebot" -hdr "Host: simple.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.status == 200


    txreq -url "/wiki/Jupiter"  -hdr "User-Agent: Googlebot" -hdr "Host: en.wikipedia.org" -hdr "X-Forwarded-Proto: https" -hdr "Via: 1.0 translate.google.com TWSFE/0.9"
    rxresp
    expect resp.status == 200

} -run
