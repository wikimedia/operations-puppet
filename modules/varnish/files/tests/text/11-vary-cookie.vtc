varnishtest "cache_text Vary: Cookie"

server s1 {
    # Response without "Vary: Cookie"
    rxreq
    expect req.url == "/no-vary-cookie"
    txresp

    # Response *with* "Vary: Cookie"
    rxreq
    expect req.url == "/vary-cookie"
    txresp -hdr "Vary: Cookie"

    # Response *with* "Vary: Cookie"
    rxreq
    expect req.url == "/vary-cookie"
    txresp -hdr "Vary: Cookie"
} -start

varnish v1 -arg "-p cc_command='exec cc -fpic -shared -Wl,-x -L/usr/local/lib/ -o %o %s -lmaxminddb' -p vcc_allow_inline_c=true -p vcc_err_unref=false" -vcl+backend {
    backend vtc_backend {
        .host = "${s1_addr}"; .port = "${s1_port}";
    }

    include "/usr/share/varnish/tests/wikimedia_text-frontend.vcl";
} -start

client c1 {
    # Session cookie, response without "Vary: Cookie"
    txreq -url "/no-vary-cookie" -hdr "Cookie: enwikiSession=42;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache ~ "miss"

    # Session cookie, response with "Vary: Cookie"
    txreq -url "/vary-cookie" -hdr "Cookie: enwikiSession=42;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache ~ "pass"

    # Non-Session cookie, response with "Vary: Cookie"
    txreq -url "/vary-cookie" -hdr "Cookie: WMF-Last-Access=25-Feb-2017;" -hdr "X-Forwarded-Proto: https"
    rxresp
    expect resp.status == 200
    expect resp.http.X-Cache ~ "miss"
} -run
