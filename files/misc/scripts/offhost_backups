#!/usr/bin/perl
use strict;
use IPC::Open3;
use IO::Select;
use Sys::Syslog;
use Getopt::Std;

# general conf
my $conf = {
	'SENDER' => 'root@wikimedia.org',
	'SUCCESS-RECIPIENT' => 'jgreen@wikimedia.org', # this recip gets only SUCCESS notices
	'FAILURE-RECIPIENT' => 'jgreen@wikimedia.org', # this recip gets only FAILURE notices
	'HOSTNAME' => `hostname --short`,
	'IDENT' => ($0 =~ /([^\/]+)$/) ? $1 : $0,
	'DATE' => `/bin/date "+%Y%m%d-%H%M%S"`,
	'LOCKFILE' => '/tmp/offhost_backups.lock',
};
chomp $conf->{'HOSTNAME'};
chomp $conf->{'DATE'};

# jobs, if it matters, keep 'em numbered in the order you want them to happen
# --ignore-existing to prevent a 'rogue' db box from clobbering previously collected dumps
# try local boxes first, especially those with few other jobs
# then cross-site boxes
my ($mirror,$email);
if ($conf->{'HOSTNAME'} eq 'indium') {

	# copy from archive peers and fundraising db dumpers
	$mirror->{'1'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@db1025.eqiad.wmnet:/a/archive/ /a/archive/';
	$mirror->{'2'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing --exclude udplogs backupmover@aluminium.wikimedia.org:/archive/ /a/archive/';
	$mirror->{'3'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@db78.pmtpa.wmnet:/a/archive/ /a/archive/';

	# copy local logs to archive peers
	$mirror->{'3'} = 'rsync -rltd --chmod=Fug-w,o-rwx /a/archive/indium/logs/ backupmover@aluminium.wikimedia.org:/archive/indium/logs/';
	$mirror->{'4'} = 'rsync -rltd --chmod=Fug-w,o-rwx /a/archive/indium/logs/ backupmover@db78.pmtpa.wmnet:/a/archive/indium/logs/';

} elsif ($conf->{'HOSTNAME'} eq 'db78') {

	# copy from archive peers and db dumpers
	$mirror->{'1'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@db1025.eqiad.wmnet:/a/archive/ /a/archive/';
	$mirror->{'2'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@aluminium.wikimedia.org:/archive/ /a/archive/';

} elsif ($conf->{'HOSTNAME'} eq 'aluminium') {

	# copy from archive peers and db dumpers
	$mirror->{'1'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@db1025.eqiad.wmnet:/a/archive/ /archive/';
	$mirror->{'2'} = 'rsync -rltd --chmod=Fg-w,o-rwx --exclude *.tmp --ignore-existing backupmover@db78.pmtpa.wmnet:/a/archive/ /archive/';

	# collect local syslog stuff from paypal to archive
	$mirror->{'3'} = "rsync -ar --exclude *log /var/log/fundraising/ /archive/$conf->{'HOSTNAME'}/logs/";

	# hacky remote snapshot strategy for Zack's script-hacking dir (probably switch this to copy to grosley...)
	## remote: make a hard-link copy of the most recent rsync destination
	$mirror->{'4'} = "/usr/bin/ssh fundraising\@tridge.wikimedia.org \"cd /data/fundraising/$conf->{'HOSTNAME'}/br; cp -al \\`ls -t|grep '^[0-9]\\{8\\}-[0-9]\\{6\\}\$'|head -1\\` $conf->{'DATE'}\"";
	## rsync over the hard-link copy to clobber whatever has changed 
	$mirror->{'5'} = "/usr/bin/rsync -ar --delete /srv/br/ fundraising\@tridge.wikimedia.org:$conf->{'HOSTNAME'}/br/$conf->{'DATE'}/";
	## remote: adjust the modified timestamp on the remote rsync dir, so next run will see it as most recent
	$mirror->{'6'} = "/usr/bin/ssh fundraising\@tridge.wikimedia.org \"touch /data/fundraising/$conf->{'HOSTNAME'}/br/$conf->{'DATE'}\"";

} elsif ($conf->{'HOSTNAME'} eq 'grosley') {

	# collect local syslog stuff from paypal to archive
	$mirror->{'1'} = "rsync -ar --exclude *log /var/log/fundraising/ /archive/$conf->{'HOSTNAME'}/logs/";

	# hacky remote snapshot strategy for Zack's script-hacking dir (probably switch this to copy to grosley...)
	## remote: make a hard-link copy of the most recent rsync destination
	$mirror->{'2'} = "/usr/bin/ssh fundraising\@tridge.wikimedia.org \"cd /data/fundraising/$conf->{'HOSTNAME'}/br; cp -al \\`ls -t|grep '^[0-9]\\{8\\}-[0-9]\\{6\\}\$'|head -1\\` $conf->{'DATE'}\"";
	## rsync over the hard-link copy to clobber whatever has changed 
	$mirror->{'3'} = "/usr/bin/rsync -ar --delete /srv/br/ fundraising\@tridge.wikimedia.org:$conf->{'HOSTNAME'}/br/$conf->{'DATE'}/";
	## remote: adjust the modified timestamp on the remote rsync dir, so next run will see it as most recent
	$mirror->{'4'} = "/usr/bin/ssh fundraising\@tridge.wikimedia.org \"touch /data/fundraising/$conf->{'HOSTNAME'}/br/$conf->{'DATE'}\"";

}

# lock to prevent collisions
my $lock_error = setlockfile('set');
printlog("already running? $lock_error") if defined $lock_error;

# collect command-line options
# opt_e = email only for errors
# opt_q = don't email at all
our ($opt_q,$opt_e);
getopts ("qe");

for my $m (sort {$a<=>$b} keys %{$mirror}) {

	printlog($mirror->{$m});
	$email->{'report'} .= "$mirror->{$m}\n";

	# open system call using open3 so we can cleanly address stderr vs stdout
	my $child_pid = open3(*W, *R, *E, $mirror->{$m});

	close (W); # don't need the write handle at all, so we close that

	# use IO::Select to step through the output of open3
	my ($selector) = IO::Select->new();
	$selector->add (*R, *E);

	# keep looping through open3 filehandles until there is no more output on any of them
	while (1) {
		last if scalar ($selector->handles) == 0;
		my @ready = $selector->can_read (1);
		for my $fh (@ready) {
			my $ffh = fileno($fh);  # identify which handle we're dealing with
			if (eof ($fh)) {	# close the filehandle if we reach the end of output
				$selector->remove ($fh);
				next;
			}
			my $line = scalar <$fh>; # this is the output itself from the current filehandle
			chomp $line;
			if ($ffh == fileno(R)) { # output on stdout
				printlog($line);
				$email->{'report'} .= "$line\n";
			} elsif ($ffh == fileno (E)) { # output on stderr
				printlog($line,'warning');
				next if $line =~ /^tar: Removing leading/;
				$email->{'report'} .= "$line\n";
				$email->{'errors'}++;
			}
		}
	}

	close (R);
	close (E);

}

# set the subject line according to the outcome
if (($email->{'errors'}) and (! defined $opt_q)) {
	$email->{'subject'} = "FAILURE: $conf->{'HOSTNAME'} $conf->{'IDENT'} had errors";
	$email->{'recipient'} = $conf->{'FAILURE-RECIPIENT'};
} elsif ((! defined $opt_e) and (! defined $opt_q)) {
	$email->{'subject'} = "SUCCESS: $conf->{'HOSTNAME'} $conf->{'IDENT'} ran without errors";
	$email->{'recipient'} = $conf->{'SUCCESS-RECIPIENT'};
}

# send message
if (defined $email->{'recipient'}) {
	open MAILER, "|/usr/sbin/sendmail -f $conf->{'SENDER'} $email->{'recipient'}";
	print MAILER "From: $conf->{'SENDER'}\n" .
		"To: $email->{'recipient'}\n" .
		"Subject: $email->{'subject'}\n\n" .
		"$email->{'report'}";
	close MAILER;
}

setlockfile('unset');

printlog('done!');

exit;


#         _                 _   _             
# ____  _| |__ _ _ ___ _  _| |_(_)_ _  ___ ___
#(_-< || | '_ \ '_/ _ \ || |  _| | ' \/ -_|_-<
#/__/\_,_|_.__/_| \___/\_,_|\__|_|_||_\___/__/
#                                             

sub printlog {
	my $msg = $_[0] ? $_[0] : '';
	my $severity = $_[1] ? $_[1] : 'info'; # notice warning error etc.
	Sys::Syslog::setlogsock('unix');
	Sys::Syslog::openlog($conf->{'IDENT'},'ndelay,pid','user');
	Sys::Syslog::syslog($severity,$msg);
	Sys::Syslog::closelog();
}


# set lockfile
sub setlockfile {
    my $action = shift;
    if ($action eq 'set') {
        return("$0 already running") if (running($0));
        open LOCK, "> $conf->{'LOCKFILE'}";
        flock (LOCK, 2) or return("$0 already running?");
        print LOCK $$;
        close LOCK;
    } else {
        unlink $conf->{'LOCKFILE'};
    }
    return;
}


# check for lockfile
#  -> if found, check for process matching PID
#   -> if no matching process, whack the lockfile
sub running {
    my $prog = shift;
    $prog =~ s/^(.*\/)+//g;
    if (-e $conf->{'LOCKFILE'}) {
        open LOCK, $conf->{'LOCKFILE'};
        my $checkpid = <LOCK>;
        close LOCK;
        if ($checkpid) {
            chomp $checkpid;
            if (`ps -p $checkpid -o command= | grep $prog`) {
                return $checkpid;
            } else {
                printlog("removed stale lockfile for $prog ($checkpid)",'warning');
                unlink $conf->{'LOCKFILE'};
            }
        }
    }
    return;
}

